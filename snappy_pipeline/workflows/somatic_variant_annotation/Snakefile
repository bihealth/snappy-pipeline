# -*- coding: utf-8 -*-
"""CUBI Pipeline somatic_variant_annotation step Snakefile"""

import os

from snappy_pipeline import expand_ref
from snappy_pipeline.workflows.somatic_variant_annotation import (
    SomaticVariantAnnotationWorkflow,
)

__author__ = "Manuel Holtgrewe <manuel.holtgrewe@bihealth.de>"


# Configuration ===============================================================


configfile: "config.yaml"


# Expand "$ref" JSON pointers in configuration (also works for YAML)
config, lookup_paths, config_paths = expand_ref("config.yaml", config)

# WorkflowImpl Object Setup ===================================================

wf = SomaticVariantAnnotationWorkflow(workflow, config, lookup_paths, config_paths, os.getcwd())
tools = wf.config["tools"]

# Rules =======================================================================


localrules:
    # Linking files from work/ to output/ should be done locally
    somatic_variant_annotation_link_out_run,


rule all:
    input:
        wf.get_result_files(),


# House-Keeping ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# Generic linking out ---------------------------------------------------------


rule somatic_variant_annotation_link_out_run:
    input:
        wf.get_input_files("link_out", "run"),
    output:
        wf.get_output_files("link_out", "run"),
    run:
        shell(wf.get_shell_cmd("link_out", "run", wildcards))


# Somatic Variant Annotation ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# Run VEP ---------------------------------------------------------------------

if "vep" in tools:

    if not wf.config["vep"]["plugins_dir"]:

        rule somatic_variant_annotation_vep_plugins:
            output:
                wf.get_output_files("vep", "plugins"),
            threads: wf.get_resource("vep", "plugins", "threads")
            resources:
                time=wf.get_resource("vep", "plugins", "time"),
                memory=wf.get_resource("vep", "plugins", "memory"),
                partition=wf.get_resource("vep", "plugins", "partition"),
                tmpdir=wf.get_resource("vep", "plugins", "tmpdir"),
            params:
                args=[
                    "https://github.com/griffithlab/pVACtools/raw/refs/heads/master/pvactools/tools/pvacseq/VEP_plugins/Frameshift.pm",
                    "https://github.com/griffithlab/pVACtools/raw/refs/heads/master/pvactools/tools/pvacseq/VEP_plugins/Wildtype.pm",
                ],
            log:
                wf.get_log_file("vep", "plugins"),
            shell:
                r"""
                    rm -f {log}
                    for url in {params.args}
                    do
                        plugin=$(basename $url)
                        fn=$(dirname {output})/$plugin
                        wget --append-output={log} --output-document=$fn $url
                    done
                    touch {output}
                """


    rule somatic_variant_annotation_vep_run:
        input:
            **wf.get_input_files("vep", "run"),
        output:
            **wf.get_output_files("vep", "run"),
        threads: wf.get_resource("vep", "run", "threads")
        resources:
            time=wf.get_resource("vep", "run", "time"),
            memory=wf.get_resource("vep", "run", "memory"),
            partition=wf.get_resource("vep", "run", "partition"),
            tmpdir=wf.get_resource("vep", "run", "tmpdir"),
        params:
            args=wf.get_args("vep", "run"),
        log:
            **wf.get_log_file("vep", "run"),
        wrapper:
            wf.wrapper_path("vep/run")
