# -*- coding: utf-8 -*-
"""CUBI Pipeline somatic_variant_annotation step Snakefile"""

import os

from snappy_pipeline import expand_ref
from snappy_pipeline.workflows.somatic_variant_annotation import (
    SomaticVariantAnnotationWorkflow,
)

__author__ = "Manuel Holtgrewe <manuel.holtgrewe@bihealth.de>"

# Configuration ===============================================================


configfile: "config.yaml"


# Expand "$ref" JSON pointers in configuration (also works for YAML)
config, lookup_paths, config_paths = expand_ref("config.yaml", config)

# WorkflowImpl Object Setup ===================================================

wf = SomaticVariantAnnotationWorkflow(workflow, config, lookup_paths, config_paths, os.getcwd())


# Modules =====================================================================


module ngs_mapping:
    snakefile:
        "../ngs_mapping/Snakefile"
    config:
        wf.w_config
    prefix:
        wf.config.get("path_ngs_mapping", "../ngs_mapping")


if not "ngs_mapping_all" in set(map(lambda r: r.name, workflow.rules)):

    use rule * from ngs_mapping


module somatic_variant_calling:
    snakefile:
        "../somatic_variant_calling/Snakefile"
    config:
        wf.w_config
    prefix:
        wf.config.get("path_somatic_variant", "../somatic_variant_calling")


use rule * from somatic_variant_calling


# Rules =======================================================================


localrules:
    # Linking files from work/ to output/ should be done locally
    somatic_variant_annotation_link_out_run,


rule somatic_variant_annotation_all:
    input:
        wf.get_result_files(),
    default_target: True


# House-Keeping ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# Generic linking out ---------------------------------------------------------


rule somatic_variant_annotation_link_out_run:
    input:
        wf.get_input_files("link_out", "run"),
    output:
        wf.get_output_files("link_out", "run"),
    run:
        shell(wf.get_shell_cmd("link_out", "run", wildcards))


# Somatic Variant Annotation ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# Run VEP ---------------------------------------------------------------------


rule somatic_variant_annotation_vep:
    input:
        **wf.get_input_files("vep", "run"),
    output:
        **wf.get_output_files("vep", "run"),
    threads: wf.get_resource("vep", "run", "threads")
    resources:
        time=wf.get_resource("vep", "run", "time"),
        memory=wf.get_resource("vep", "run", "memory"),
        partition=wf.get_resource("vep", "run", "partition"),
        tmpdir=wf.get_resource("vep", "run", "tmpdir"),
    params:
        args=wf.get_args("vep", "run"),
    log:
        **wf.get_log_file("vep", "run"),
    wrapper:
        wf.wrapper_path("vep/run")
